/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import at.bxm.gradleplugins.svntools.tasks.SvnCheckout
import org.apache.ivy.util.url.ApacheURLLister

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.apache.ivy:ivy:2.3.0'
    }
}

plugins {
    id "de.undercouch.download" version "3.4.3"
    id "at.bxm.svntools" version "2.2.1"
}

apply plugin: 'base'

def taskGroup = 'Release Verification'
def verificationDir = mkdir("$buildDir/dist")
def releaseTagDir = mkdir("$buildDir/tag")

task downloadReleaseNotes(type: Download) {
    group = taskGroup
    description = 'Downloads the RELEASE-NOTES.txt file'
    src "$distArea/RELEASE-NOTES.txt"
    dest verificationDir
}

task downloadSource(type: Download) {
    group = taskGroup
    description = 'Downloads the source artifacts'
    src listFiles("$distArea/source")
    dest mkdir("$verificationDir/source")
}

task downloadBinaries(type: Download) {
    group = taskGroup
    description = 'Downloads the binary artifacts'
    src listFiles("$distArea/binaries")
    dest mkdir("$verificationDir/binaries")
}

task downloadDistArtifacts(dependsOn: [downloadReleaseNotes, downloadSource, downloadBinaries]) {
    group = taskGroup
    description = 'Downloads all artifacts from the distribution area'
}

task checkoutSvnReleaseTag(type: SvnCheckout) {
    group taskGroup
    description = 'Checks out the SVN release tag'
    svnUrl = tag
    workspaceDir = releaseTagDir
}

task verifyRelease(dependsOn: downloadDistArtifacts) {
    group = taskGroup
    description = 'Executes the whole release verification logic'
}

def static listFiles(String url) {
    def urlLister = new ApacheURLLister()
    return urlLister.listFiles(new URL(url))
}
